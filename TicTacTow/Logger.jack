class Logger{
    field int topLeftX;
    field int topLeftY;
    field int bottomRightX;
    field int bottomRightY;
    field int BORDER_THICKNESS;
    field int CHAR_WIDTH; //in pixels
    field int CHAR_HEIGHT; //in pixels
    field int FIRST_MESSAGE_ROW; // in which row messages will be written (0 => above top-border).
    field int SECOND_MESSAGE_ROW;
    field int MESSAGE_COL_SPACE;
    field int BOARD_ROW;
    field int BOARD_COL;
    

    constructor Logger new(int AtopLeftX, int AtopLeftY, int AbottomRightX, int AbottomRightY) {
        let topLeftX = AtopLeftX;
        let topLeftY = AtopLeftY;
        let bottomRightX = AbottomRightX;
        let bottomRightY = AbottomRightY;
        let CHAR_WIDTH = 8;
        let CHAR_HEIGHT = 13;
        let FIRST_MESSAGE_ROW = 2;
        let SECOND_MESSAGE_ROW = 3;
        let MESSAGE_COL_SPACE = 1;
        let BORDER_THICKNESS = 3;
        let BOARD_ROW = 2;
        let BOARD_COL = 40;
        return this;
    }

    method void drawBorders(){
        //draw horiazonal borders
        do Screen.drawRectangle(topLeftX,topLeftY,bottomRightX,topLeftY + BORDER_THICKNESS);
        do Screen.drawRectangle(topLeftX,bottomRightY - BORDER_THICKNESS,bottomRightX,bottomRightY);
        //draw vertical bordres
        do Screen.drawRectangle(topLeftX,topLeftY,topLeftX + BORDER_THICKNESS,bottomRightY);
        do Screen.drawRectangle(bottomRightX-BORDER_THICKNESS,topLeftY,bottomRightX,bottomRightY);
        return;
    }

    method void setCursor(int col_space, int row_space){
        var int col;
        var int row;
        let col = Math.divide(topLeftX,CHAR_WIDTH) + col_space; 
        let row = Math.divide(topLeftY,CHAR_HEIGHT)+ row_space;
        do Output.moveCursor(row,col);
        return;
    }

    method int getTurn(int player){
        var int choise;
        do setCursor(MESSAGE_COL_SPACE,FIRST_MESSAGE_ROW);
        do Output.printString("player");
        do Output.printInt(player);
        let choise = Keyboard.readInt(" choose your move:");
        return choise;
    }

    method void printWinner(int winner){
        do setCursor(MESSAGE_COL_SPACE,SECOND_MESSAGE_ROW);
        if(winner=0){
            do Output.printString("--DRAW--");
        }
        else{
            do Output.printString("The winner is player");
            do Output.printInt(winner);
            do Output.printString(" !!!!!");
        }
        return;
    }

    method void printErrorSpotMessage(int choice){
        do setCursor(MESSAGE_COL_SPACE,SECOND_MESSAGE_ROW);
        do Output.printString("Spot ");
        do Output.printInt(choice);
        do Output.printString(" is illegal!!!!");
        return; 
    }

    method void clearErrorMessage(){
        do setCursor(MESSAGE_COL_SPACE,SECOND_MESSAGE_ROW);
        do Output.printString("                       ");
        return;
    }


    /*print the bord as characters. top-left corner is located by x,y
     * where:
     * y is col (in range 0..63)
     * x is row (in range 0..22)
    /* example:
     *|0|1|2|
     *|3|4|5|
     *|6|7|8|   
     */
    method void printBoard(){ 
        var int x;
        var int y;
        var int i;
        var int j;
        let x = BOARD_ROW;
        let y = BOARD_COL;
        let i = 0;
        let j = 0;
        do setCursor(BOARD_COL,BOARD_ROW);
        while(i < 3){
            do Output.moveCursor(x+i,y);
            let j = 0;
            do Output.printString("|");
            while(j < 3)
            {
                do Output.printInt(j+Math.multiply(3,i));
                do Output.printString("|");
                let j = j+1;
            }
            do Output.println();
            let i = i+1;
        }
        return;
    }



}